package Wordwonk::Schema::Result::Game;
use Moose;
use MooseX::NonMoose;
extends 'DBIx::Class::Core';

use Mojo::JSON;
use Mojo::Util qw(encode decode);
__PACKAGE__->table('games');
__PACKAGE__->load_components(qw/InflateColumn::DateTime TimeStamp/);
__PACKAGE__->add_columns(
    id => {
        data_type => 'uuid',
        is_nullable => 0,
    },
    rack => {
        data_type => 'text[]',
        is_nullable => 0,
    },
    letter_values => {
        data_type => 'jsonb',
        is_nullable => 0,
    },
    started_at => {
        data_type => 'timestamp with time zone',
        is_nullable => 1,
    },
    created_at => {
        data_type => 'timestamp with time zone',
        set_on_create => 1,
    },
    finished_at => {
        data_type => 'timestamp with time zone',
        is_nullable => 1,
    },
    mutant_letter => {
        data_type => 'varchar',
        size => 2,
        is_nullable => 1,
    },
    language => {
        data_type => 'varchar',
        size => 10,
        is_nullable => 0,
        default_value => 'en',
    }
);

__PACKAGE__->set_primary_key('id');
__PACKAGE__->inflate_column('letter_values', {
    inflate => sub { my $v = shift; return undef unless defined $v; Mojo::JSON::decode_json(encode('UTF-8', $v)) },
    deflate => sub { my $v = shift; return undef unless defined $v; decode('UTF-8', Mojo::JSON::encode_json($v)) },
});

__PACKAGE__->inflate_column('rack', {
    inflate => sub {
        my $val = shift;
        return $val if ref $val eq 'ARRAY';
        # Postgres text[] comes back as {A,B,C}
        $val =~ s/^\{(.*)\}$/$1/;
        return [ split /,/, $val ];
    },
    deflate => sub { shift },
});

__PACKAGE__->has_many(
    plays => 'Wordwonk::Schema::Result::Play',
    'game_id'
);

__PACKAGE__->meta->make_immutable(inline_constructor => 0);

1;

